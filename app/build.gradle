apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.lonchi.andrej.lonchi_bakalarka"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode versionCodeFromDate()
        versionName "0.0.1"
        vectorDrawables.useSupportLibrary true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "API_URL", "\"https://domain.com/api/\""

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
            javacOptions {
                // Increase the max count of errors from annotation processors. Default is 100.
                option("-Xmaxerrs", 500)
            }
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    dataBinding {
        enabled = true
    }

    buildFeatures {
        viewBinding true
    }

    dokka {
        outputFormat = 'html'
        outputDirectory = "$buildDir/kotlindoc"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    signingConfigs {
        //debugConfig {
        //    storeFile file("debug.keystore")
        //    storePassword "android"
        //    keyAlias "testreleasekey"
        //    keyPassword "android"
        //}
        releaseConfig {
            storeFile file("bakalarka.jks")
            storePassword "bakalarka"
            keyAlias "key0"
            keyPassword "bakalarkakey"
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            buildConfigField "boolean", "DEBUG_MODE", "true"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            debuggable false
            minifyEnabled false
            buildConfigField "boolean", "DEBUG_MODE", "false"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //  Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    //  UI
    implementation "com.google.android.material:material:$rootProject.ext.material"
    implementation "androidx.appcompat:appcompat:$rootProject.ext.appCompat"
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.ext.constraintLayout"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.core:core-ktx:$rootProject.ext.coreKtx"

    //  Tests
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    //  Material dialogs
    implementation "com.afollestad.material-dialogs:core:$rootProject.ext.materialDialogs"

    //  Navigation component
    def navigation = "2.3.1"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation"

    // Architecture Components
    implementation "androidx.lifecycle:lifecycle-extensions:$rootProject.ext.lifecycleVersion"
    kapt "androidx.lifecycle:lifecycle-compiler:$rootProject.ext.lifecycleVersion"
    testImplementation "androidx.arch.core:core-testing:$rootProject.ext.lifecycleVersion"

    // Room
    def roomVersion = "2.2.5"
    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"

    // Dagger
    def dagger = "2.25.2"
    implementation "com.google.dagger:dagger-android:$dagger"
    implementation "com.google.dagger:dagger-android-support:$dagger"
    kapt "com.google.dagger:dagger-compiler:$dagger"
    kapt "com.google.dagger:dagger-android-processor:$dagger"

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.6.0'
    implementation "com.squareup.retrofit2:converter-moshi:2.3.0"
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'

    // RxJava
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.0.0'
    implementation 'com.jakewharton.rxbinding3:rxbinding:3.1.0'

    // Timber
    implementation "com.jakewharton.timber:timber:$rootProject.ext.timber"

    //  Firebase
    implementation platform('com.google.firebase:firebase-bom:26.0.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.android.gms:play-services-auth:19.0.0'

    //  For some reason with this BoM dependency build failed
    //implementation 'com.google.firebase:firebase-ml-vision'
    implementation 'com.google.android.gms:play-services-vision:20.1.2'
    implementation 'com.google.firebase:firebase-ml-vision:24.1.0'

    //  Emojis
    implementation "com.android.support:support-emoji:28.0.0"

    //  Camera X
    def camerax_version = "1.0.0-beta11"
    implementation "androidx.camera:camera-camera2:$camerax_version"
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    implementation "androidx.camera:camera-view:1.0.0-alpha18"

    //Lottie
    implementation 'com.airbnb.android:lottie:3.4.0'

    //  Paracamera
    implementation 'com.mindorks:paracamera:0.2.2'

    //  Glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

    //  Coil
    implementation("io.coil-kt:coil:1.1.1")
}

static def versionCodeFromDate() {
    def date = new Date()
    def formattedDate = date.format('yyMMddHHmm')
    return Integer.valueOf(formattedDate)
}

apply plugin: 'com.google.gms.google-services'